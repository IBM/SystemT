/*******************************************************************************
* Copyright IBM
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*     http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*******************************************************************************/

module module1; 
-- TODO: Add AQL content here 

--*****************************************************************************
-- BigInsights TextAnalytics sample Indicator extractor
-- 
-- File name: aql/Indicator/concepts.aql
-- Description: Concept Generation rules
--*****************************************************************************

-- Identify mentions of absolute amounts as a sequence of '$' character, 
-- followed by a Number mention, optionally followed by a Unit mention
-- Example: $7 billion, $11.52
create view AmountAbsolute as
extract pattern /\$/ <N.match> <U.match>?
	return group 0 as match
from Number N, Unit U
consolidate on match;


-- Identify mentions of percentage amounts as a sequence of Number mention 
-- followed by the token 'percent'
-- Example: 49 percent, 46.1 percent
create view AmountPercent as
extract pattern <N.match> 'percent' 
	return group 0 as match
from Number N;


-- Union all absolute and percentage amount candidates
-- Example: $7 billion, $11.52, 49 percent, 46.1 percent
create view AmountCandidate as
(select R.* from AmountAbsolute R)
union all
(select R.* from AmountPercent R);  


-- Negative clues that signal a relative amount
-- Example: increased, decreased, down, up
create dictionary AmountNegativeClueDict
from file 'dictionaries/amount_negativeClue.dict'
with language as 'en';


-- Filter out amounts that are relative (preceded by a negative clue) from 
-- the set of all Amount candidates
-- Example: increased 11 percent, down 2 percent
create view Amount as
select R.* 
from AmountCandidate R
where Not(MatchesDict('AmountNegativeClueDict', LeftContextTok(R.match,1)));


-- Identify candidate indicators as a mention of metric followed within 
-- 0 to 10 tokens of a mention of amount
-- Example: Gross profit margin of 46.1 percent, cash flow of $8.7 billion
-- type 1A
create view IndicatorCandidate as
select M.match as metric, A.match as amount, CombineSpans(M.match, A.match) as match 
from Metric M, Amount A
where FollowsTok(M.match, A.match, 0, 10);

export view IndicatorCandidate;

